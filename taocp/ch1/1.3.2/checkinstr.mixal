* 1.3.2-9: write a MIX program to determine if a MIX instruction (stored as a word) is valid
* Each table entry consists of up to two checks to perform: on the F and A-fields.
* Main program
PRINTER	EQU	18
INSTR	EQU	1000
	ORIG	3000
PREP	ENTA	2000		Load a test case
	STA	INSTR(0:2)
	ENTA	0
	STA	INSTR(3:3)
	ENTA	28
	STA	INSTR(4:4)
	ENTA	63
	STA	INSTR(5:5)
START	IOC	0(PRINTER)
	LD4	INSTR(3:3)	I4 = I field
	LD5	INSTR(4:4)  	I5 = F field
	LDA	INSTR(5:5)	A  = C field
CHECKI	DEC4	6		Check that 0<=I<=6
	J4P	BAD
CHECKC	DECA	63		Check that 0<=C<=63
	JAP	BAD
* Index into the table
  	INCA	63
	STA	CVAL
	ADD	CVAL
	STA	CVAL
	LD6	CVAL		I6 = 2*A
	JMP	TABLE,6
* Subroutines for checking the F field
FSPEC	STJ	0F	  	Let F=(A:B)
	ENTA	0
	LDX	INSTR(4:4)
	DIV	=8=		
	STX	SPECTO   	Check A<=B
	CMPA	SPECTO
	JG	BAD
	DECA	5		Check 0<=A<=5
	JAP	BAD
	DECX	5		Check 0<=B<=5
	JXP	BAD
0H	JMP	*
F0TO2	STJ	0F
	DEC5	2		Check 0<=F<=2
	J5P	BAD
0H	JMP	*
F0TO5	STJ	0F
	DEC5	5		Check 0<=F<=5
	J5P	BAD
0H	JMP	*
F0TO20	STJ	0F
	DEC5	20		Check 0<=F<=20
	J5P	BAD
0H	JMP	*
F0TO9	STJ	0F
	DEC5	9		Check 0<=F<=9
	J5P	BAD
0H	JMP	*
F0TO3	STJ	0F
	DEC5	3		Check 0<=F<=3
	J5P	BAD
0H	JMP	*
*
CHECKA	LD4	INSTR(3:3)
	J4N	GOOD		Address is automatically valid when I!=0
	LD3	INSTR(0:2)  	I3 = A field
	J3N	BAD		Check that 0<=A<=3999
	DEC3	3999
	J3P	BAD
	JMP	GOOD
GOOD	OUT	GOODMSG(PRINTER)
	HLT
BAD	OUT	BADMSG(PRINTER)
	HLT
*
DATA	EQU	0
	ORIG	DATA
GOODMSG	ALF	VALID
	ALF	 INST
	ALF	RUCTI
	ALF	ON :D
	ORIG	GOODMSG+24
BADMSG	ALF	INVAL
	ALF	ID IN
	ALF	STRUC
	ALF	TION 
	ALF	:(   
	ORIG	BADMSG+24
TABLE	NOP	0		NOP
	JMP	GOOD
	JMP	FSPEC	ADD
	JMP	CHECKA
	JMP	FSPEC	SUB
	JMP	CHECKA
	JMP	FSPEC	MUL
	JMP	CHECKA
	JMP	FSPEC	DIV
	JMP	CHECKA
	JMP	F0TO2	NUM/CHAR/HLT
	JMP	GOOD
	JMP	F0TO5	SLA/SRA/SLAX/SRAX/SLC/SRC
	JMP	GOOD
	NOP	0		MOVE
	JMP	CHECKA
	JMP	FSPEC	LDA
	JMP	CHECKA
	JMP	FSPEC	LD1
	JMP	CHECKA
	JMP	FSPEC	LD2
	JMP	CHECKA
	JMP	FSPEC	LD3
	JMP	CHECKA
	JMP	FSPEC	LD4
	JMP	CHECKA
	JMP	FSPEC	LD5
	JMP	CHECKA
	JMP	FSPEC	LD6
	JMP	CHECKA
	JMP	FSPEC	LDX
	JMP	CHECKA
	JMP	FSPEC	LDAN
	JMP	CHECKA
	JMP	FSPEC	LD1N
	JMP	CHECKA
	JMP	FSPEC	LD2N
	JMP	CHECKA
	JMP	FSPEC	LD3N
	JMP	CHECKA
	JMP	FSPEC	LD4N
	JMP	CHECKA
	JMP	FSPEC	LD5N
	JMP	CHECKA
	JMP	FSPEC	LD6N
	JMP	CHECKA
	JMP	FSPEC	LDXN
	JMP	CHECKA
	JMP	FSPEC	STA
	JMP	CHECKA
	JMP	FSPEC	ST1
	JMP	CHECKA
	JMP	FSPEC	ST2
	JMP	CHECKA
	JMP	FSPEC	ST3
	JMP	CHECKA
	JMP	FSPEC	ST4
	JMP	CHECKA
	JMP	FSPEC	ST5
	JMP	CHECKA
	JMP	FSPEC	ST6
	JMP	CHECKA
	JMP	FSPEC	STX
	JMP	CHECKA
	JMP	FSPEC	STJ
	JMP	CHECKA
	JMP	FSPEC	STZ
	JMP	CHECKA
	JMP	F0TO20	JBUS
	JMP	GOOD
	JMP	F0TO20	IOC
	JMP	GOOD
	JMP	F0TO20	IN
	JMP	CHECKA
	JMP	F0TO20	OUT
	JMP	CHECKA
	JMP	F0TO20	JRED
	JMP	GOOD
	JMP	F0TO9	JMP/JSJ/JOV/JNOV/JL/E/JG/JGE/JNE/JLE
	JMP	CHECKA
	JMP	F0TO5	JAN/JAZ/JAP/JNN/JANZ/JANP
	JMP	CHECKA
	JMP	F0TO5	J1N/J1Z/J1P/JNN/J1NZ/J1NP
	JMP	CHECKA
	JMP	F0TO5	J2N/J2Z/J2P/JNN/J2NZ/J2NP
	JMP	CHECKA
	JMP	F0TO5	J3N/J3Z/J3P/JNN/J3NZ/J3NP
	JMP	CHECKA
	JMP	F0TO5	J4N/J4Z/J4P/JNN/J4NZ/J4NP
	JMP	CHECKA
	JMP	F0TO5	J5N/J5Z/J5P/JNN/J5NZ/J5NP
	JMP	CHECKA
	JMP	F0TO5	J6N/J6Z/J6P/JNN/J6NZ/J6NP
	JMP	CHECKA
	JMP	F0TO5	JXN/JXZ/JXP/JNN/JXNZ/JXNP
	JMP	CHECKA
	JMP	F0TO3	INCA/DECA/ENTA/ENNA
	JMP	GOOD
	JMP	F0TO3	INC1/DEC1/ENT1/ENN1
	JMP	GOOD
	JMP	F0TO3	INC2/DEC2/ENT2/ENN2
	JMP	GOOD
	JMP	F0TO3	INC3/DEC3/ENT3/ENN3
	JMP	GOOD
	JMP	F0TO3	INC4/DEC4/ENT4/ENN4
	JMP	GOOD
	JMP	F0TO3	INC5/DEC5/ENT5/ENN5
	JMP	GOOD
	JMP	F0TO3	INC6/DEC6/ENT6/ENN6
	JMP	GOOD
	JMP	F0TO3	INCX/DECX/ENTX/ENNX
	JMP	GOOD
	JMP	FSPEC	CMPA
	JMP	CHECKA
	JMP	FSPEC	CMP1
	JMP	CHECKA
	JMP	FSPEC	CMP2
	JMP	CHECKA
	JMP	FSPEC	CMP3
	JMP	CHECKA
	JMP	FSPEC	CMP4
	JMP	CHECKA
	JMP	FSPEC	CMP5
	JMP	CHECKA
	JMP	FSPEC	CMP6
	JMP	CHECKA
	JMP	FSPEC	CMPX
	JMP	CHECKA
	END	PREP