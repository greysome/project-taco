* 2.5-4: implement Algorithm A (first-fit method)
* I use the modified step A4'.
*
* Input: rI1=N
* Output: rI5=L
* Registers used:
* rI2=P, rI3=Q, rI4=K, rI5=tmp
LINK	EQU	0:2
SIZE	EQU	4:5
FSTFIT	STJ	9F
A1	ENT3	AVAIL		Q <- LOC(AVAIL).
A2	LD2	0,3(LINK)	P <- LINK(Q).
	J2N	OVERFLOW	P = NULL?
A3	CMP1	0,2(SIZE)
	JLE	A4		N <= SIZE(P)?
	ENT3	0,2		Q <- P.
	JMP	A2
A4	LD4	0,2(SIZE)	K
	DEC4	0,1		  <- SIZE(P) - N.
	CMP4	=10=
	JGE	1F		K >= c?
	LD5	0,2(LINK)	LINK(P)
	ST5	0,3(LINK)	  -> LINK(Q).
	ENT5	0,2		L <- P.
	JMP	9F
1H	ST4	0,2(SIZE)	SIZE(P) <- K.
	INC2	0,4		P+K
	ENT5	0,2		  -> L.
	ST1	0,5(SIZE)	SIZE(L) <- N.
9H	JMP	*
	END	FSTFIT