* Program D from 2.3.2, using my solutions to the exercises
*
* NOTE: this has not been tested. On the other hand,
* differentiate_original_with_test.mixal (using Knuth's solution) has
* been tested.
*
* DIFFERENTIATION IN A RIGHT-THREADED TREE
LLINK	EQU	4:5
RLINK	EQU	1:2
RLINKT	EQU	0:2
TYPE	EQU	3:3
* MAIN CONTROL ROUTINE
D1	STJ	9F
	LD4	Y(LLINK)
1H	ENT2	0,4
2H	LD4	0,2(LLINK)
	J4NZ	1B
D2	LD1	0,2(TYPE)
	JMP	*+1,1
	JMP	CONSTANT
	JMP	VARIABLE
	JMP	LN
	JMP	NEG
	JMP	ADD
	JMP	SUB
	JMP	MUL
	JMP	DIV
	JMP	PWR
D3	ST3	0,4(RLINK)
D4	ENT3	0,2
	LD2	0,2(RLINKT)
	J2N	1F
	ST5	0,3(RLINK)
	JMP	2B
1H	ENN2	0,2
D5	ENT1	-Y,2
	LD4	0,2(LLINK)
	LD6	0,4(RLINK)
	J1NZ	D2
	ST5	DY(LLINK)
	ENNA	DY
	STA	0,5(RLINKT)
9H	JMP	*
* BASIC SUBROUTINES FOR TREE CONSTRUCTION
TREE0	STJ	9F
	JMP	2F
TREE1	ST1	3F(0:2)
	JSJ	1F
TREE2	STX	3F(0:2)
3H	ST1	*(RLINKT)
	STJ	9F
	LDXN	AVAIL
	JXZ	OVERFLOW
	STX	0,1(RLINKT)
	LDX	3B(0:2)
	STA	*+1(0:2)
	STX	*(LLINK)
2H	LD1	AVAIL
	J1Z	OVERFLOW
	LDX	0,1(LLINK)
	STX	AVAIL
	STA	*+1(0:2)
	MOVE	*(2)
	DEC1	2
9H	JMP	*
COPYP1	ENT1	0,4
	JSJ	COPY
COPYP2	ENT1	0,3
	STJ	9F
* MY SOLUTION TO 2.3.2-13
COPY	STJ	9F
	ST1	HEAD		Save registers.
	ST2	7F(0:2)
	ENT2	0,1		P <- HEAD.
	ST3	8F(0:2)
	LD1	AVAIL		Q <= AVAIL.
	J1Z	OVERFLOW
	LDA	0,1(LLINK)
	STA	AVAIL
	STZ	0,1
	ST1	0,1(RLINKT)	RLINK(Q) <- Q, RTAG(Q) <- 0.
	JMP	C3
C2	LDA	0,2(RLINKT)
	JAN	1F		P has right child?
	LD3	AVAIL		R <= AVAIL.
	J3Z	OVERFLOW
	LDA	0,3(LLINK)
	STA	AVAIL
	STZ	0,3
	LDA	0,1(RLINKT)	RLINKT(Q)
	STA	0,3(RLINKT)	  -> RLINKT(R).
	ST3	0,1(RLINKT)	RLINK(Q) <- R, RTAG(Q) <- 0.
	JMP	C3
1H	LDAN	0,1		P does not have right child.
	JANN	C3		Jump if RTAG(Q)=1 already.
	STA	0,1		RTAG(Q) <- 1.
C3	LDA	1,2		INFO(P)
	STA	1,1		  -> INFO(Q).
	LDA	0,2(TYPE)	TYPE(P)
	STA	0,1(TYPE)	  -> TYPE(Q).
C4	LDA	0,2(LLINK)
	JAZ	1F		P has left child?
	LD3	AVAIL		R <= AVAIL.
	J3Z	OVERFLOW
	LDA	0,3(LLINK)
	STA	AVAIL
	STZ	0,3
	ST3	0,1(LLINK)	LLINK(Q) <- R.
	ENN1	0,1
	ST1	0,3(RLINKT)	RLINK(R) <- Q, RTAG(R) <- 1.
	ENN1	0,1
C5	LD1	0,1(LLINK)	Q <- LLINK(Q).
	LD2	0,2(LLINK)	P <- LLINK(P).
	JMP	C6
1H	LDA	0,2
	JANN	1F		RTAG(P) = 0?
	LD1	0,1(RLINK)	Q <- RLINK(Q).
	LD2	0,2(RLINK)	P <- RLINK(P).
	JMP	1B
1H	LD1	0,1(RLINK)	Q <- RLINK(Q).
	LD2	0,2(RLINK)	P <- RLINK(P).
C6	CMP2	HEAD		P = HEAD?
	JNE	C2
7H	ENT2	*
8H	ENT3	*
9H	JMP	*
HEAD	CON	0
CON0	CON	0
	CON	0
CON1	CON	0
	CON	1
CON2	CON	0
	CON	2
LOG	CON	2(TYPE)
	ALF     LN
NEGOP	CON	3(TYPE)
	ALF    NEG
PLUS	CON	4(TYPE)
	ALF     + 
MINUS	CON	5(TYPE)
	ALF     - 
TIMES	CON	6(TYPE)
	ALF     * 
SLASH	CON	7(TYPE)
	ALF     / 
UPARROW	CON	8(TYPE)
	ALF     **
* DIFFERENTIATION ROUTINES
VARIABLE	LDX	1,2
	ENTA	CON1
	CMPX	2F
	JE	*+2
CONSTANT	ENTA	CON0
	JMP	TREE0
1H	ENT5	0,1
	JMP	D4
2H	ALF      X
LN	LDA	1,5
	JAZ	D4
	JMP	COPYP1
	ENTX	0,5
	ENTA	SLASH
	JMP	TREE2
	JMP	1B
NEG	LDA	1,5
	JAZ	D4
	ENTA	NEGOP
	ENT1	0,5
	JMP	TREE1
	JMP	1B
ADD	LDA	1,6
	JANZ	1F
3H	LDA	AVAIL
	STA	0,6(LLINK)
	ST6	AVAIL
	JMP	D3
1H	LDA	1,5
	JANZ	1F
2H	LDA	AVAIL
	STA	0,5(LLINK)
	ST5	AVAIL
	ENT5	0,6
	JMP	D3
1H	ENTA	PLUS
4H	ENTX	0,6
	ENT1	0,5
	JMP	TREE2
	ENT5	0,1
	JMP	D3
SUB	LDA	1,5
	JAZ	2B
	LDA	1,6
	JANZ	1F
	ENTA	NEGOP
	ENT1	0,5
	JMP	TREE1
	ENT5	0,1
	JMP	3B
1H	ENTA	MINUS
	JMP	4B
MUL	LDA	1,6
	JAZ	1F
	JMP	COPYP2
	ENTA	0,6
	JMP	MULT
	ENT6	0,1
1H	LDA	1,5
	JAZ	ADD
	JMP	COPYP1
	ENTA	0,1
	ENT1	0,5
	JMP	MULT
	ENT5	0,1
	JMP	ADD
MULT	STJ	9F
	STA	1F(0:2)
	ST2	8F(0:2)
1H	ENT2	*
	LDA	1,2
	DECA	1
	JANZ	1F
	LDA	0,2(TYPE)
	JAZ	2F
1H	LDA	1,1
	DECA	1
	JANZ	1F
	LDA	0,1(TYPE)
	JANZ	1F
	ST1	*+2(0:2)
	ENT1	0,2
	ENT2	*
2H	LDA	AVAIL
	STA	0,2(LLINK)
	ST2	AVAIL
	JMP	8F
1H	ENTA	TIMES
	ENTX	0,2
	JMP	TREE2
8H	ENT2	*
9H	JMP	*
* MY SOLUTION TO 2.3.2-15
DIV	LDA	1,6		INFO(Q1)
	JAZ	1F		  != 0?
	ENTA	PLUS
	ENTX	0,6
	JMP	COPYP2
	JMP	TREE2		rI1 <- TREE(+,Q1,COPY(P2)).
	ENT6	0,1		Q1 <- rI1.
1H	LDA	1,5		INFO(Q)
	JAZ	SUB		  != 0?
	JMP	COPYP1
	ENTA	0,1
	ENT1	0,5
	JMP	MULT		rI1 <- MULT(COPY(P1),Q).
	ST1	3F(0:2)
	ENTA	CON2
	JMP	TREE0		rI1 <- TREE(2).
	ST1	2F(0:2)
	ENTA	UPARROW
	JMP	COPYP2
	ENTX	0,1
2H	ENT1	*
	JMP	TREE2		rI1 <- TREE(**,COPY(P2),TREE(2)).
	ENTA	SLASH
3H	ENTX	*
	JMP	TREE2		rI1 <- TREE(/,MULT(...),TREE(**,...)).
	ENT5	0,1		Q <- rI1.
1H	JMP	SUB
* MY SOLUTION TO 2.3.2-16
PWR	LDA	1,6		INFO(Q1)
	JAZ	4F		  != 0?
	JMP	COPYP1		rI1 <- COPY(P1).
	LDA	0,3(TYPE)	TYPE(P2)
	JANZ	2F		  = 0?
	LDA	1,3		INFO(P2)
	DECA	2
	JAZ	2F		  != 2?
	ST1	1F(0:2)
	ENTX	0,1
	INCA	1		rA <- INFO(P2) - 1.
	JMP	TREE0		rI1 <- TREE(rA).
	ENTA	UPARROW
1H	ENTX	*
	JMP	TREE2		rI1 <- TREE(**,R,TREE(INFO(P2)-1)).
	JMP	3F
2H	LDA	0,4(TYPE)	TYPE(P2)
	JAZ	3F		  = 0?
	ST1	1F(0:2)
	JMP	COPYP2
	ENTX	0,1
	ENTA	CON1
	JMP	TREE0
	ENTA	MINUS
	JMP	TREE2		rI1 <- TREE(-,COPY(P2),TREE(1)).
	ENTA	UPARROW
1H	ENTX	*
	JMP	TREE2		rI1 <- TREE(**,R,TREE(-,...)).
3H	ST1	1F(0:2)
	JMP	COPYP2
	ENTA	0,1
1H	ENT1	*
	JMP	MULT		rI1 <- MULT(COPY(P2),R).
	ENTA	0,6
	JMP	MULT		rI1 <- MULT(Q1,MULT(COPY(P2),R)).
	ENT6	0,1		Q1 <- rI1.
4H	LDA	1,5		INFO(Q)
	JAZ	ADD		  != 0?
	JMP	COPYP1
	ENTX	0,1
	JMP	COPYP2
	ENTA	UPARROW
	JMP	TREE2		rI1 <- TREE(**,COPY(P1),COPY(P2)).
	ST1	1F(0:2)
	ENTA	LOG
	JMP	COPYP1
	JMP	TREE1		rI1 <- TREE(ln,COPY(P1)).
	ENTA	0,1
	ENT1	0,5
	JMP	MULT		rI1 <- MULT(TREE(ln,...),Q).
	ENTX	0,1
	ENTA	MUL
1H	ENT1	*
	JMP	TREE2		rI1 <- TREE(*,MULT(...),TREE(**,...)).
	ENT5	0,1		Q <- rI1.
	JMP	ADD